% 000890: "Men det har vi ikke klart å vise i de siste kvalifiseringene, sier Portsmouth-spilleren."
rule double_subject {
    global {
        sent_id = "000890";
        text = re".*det har vi ikke klart å vise.*"
    }
    pattern {
        V [form="klart", upos="VERB"];
        D [form="det", upos="PRON"];
        e: V -[SUBJ]-> D;
        R [form="har", upos="AUX"];
        * -[FINV]-> R;
    }
    commands {
        del_edge e;
        add_edge f: R -> D;
        f.label = FSUBJ;
    }
}

% 8128: "- At andre regissører kaller deg pompøs ; kan det også skyldes misunnelse ?"
% SUBJ -> FSUBJ; Mulig analyse som setningsantisiperende hvis leddsetningene var omrokkert.
rule relabel_formal_subject {
    global {
        sent_id = "008128";
        text = re".*andre regissører kaller deg pompøs.*"
    }
    pattern {
        N1 [upos="PRON"];
        N2 [upos="AUX"];
        e: N2 -[SUBJ]-> N1;
    }
    with { e.length = 1; }
    commands {
        e.label = FSUBJ;
    }
}

% 007036: "(...) å utgi vår neste plate på CD - og (...)"
rule DET_to_NOUN {
    global {
        sent_id = "007036";
        text = re".*utgi vår neste plate på CD.*"
    }
    pattern {
        N [form="CD", upos="DET"];
        A [upos=ADP];
        e: A -[IK]-> N;
    }
    commands {
        N.upos = "NOUN";
        e.label = "PUTFYLL";
    }
}

% 018430: Rune Stølås / bt.no
% 001021: "Eva Joly + Daniel Cohn-Bendit = EU"
rule PAR_to_KOORD {
    global {
        sent_id = "001021"|"018430";
    }
    pattern {
        N1 [upos=PROPN];
        N3 [upos= PROPN];
        e: N1 -[PAR]-> N3;
    }
    with {
        S [form="="|"/", upos=SYM];
        * -[IK]-> S;
        N1 << S; S < N3; % The symbol immediately precedes the coordinated node
    }
    commands {
        e.label = KOORD;
    }
}


% Eksempel 000016: Først i prosesjonen gikk kong Harald og brudens mor , Marit Tjessem.
rule add_morph_feats {
    pattern {
        N [upos=NOUN, lemma="kong", !Definite, !Gender, !Number];
        * -[APP]-> N;
    }
    commands {
        N.Definite=Ind;
        N.Gender=Masc;
        N.Number=Sing;
    }
}

% 5092	 Til informasjon , så veier jeg omtrent 65 kilo - 63 kilo dersom jeg vasker meg .
rule shift_APP {
    global {
        sent_id = "005092";
        text = re".*så veier jeg omtrent 65 kilo.*"
    }
    pattern {
        A [form="så"];
        e1: V -[APP]-> A;
        e2: A -[ADV]-> N;
    }
    commands {
        e1.label = "ADV";
        e2.label = "APP";
    }

}


% 10996: "Ikke det at boka mi skal være en fasit , men den kan være underlag for å diskutere ."
% Dobbelt subjekt dersom "den" beholder "SUBJ"-relasjonen
rule APP_to_ADV {
    global {
        sent_id = "010996";
        text = re"Ikke det at boka mi skal være en fasit.*"
    }
    pattern {
        N1 [lemma="ikke"];
        N2 [form="det"];
        e: N2 -[APP]-> N1;
    }
    commands {
        e.label = ADV;
    }
}

% Prosesjonen ble avsluttet av den 90 år gamle prins Carl Bernadotte som førte brudgommens søster , Märtha Louise , til bords .
rule APP_modifiers {
    global { text = re".*den 90 år gamle prins Carl Bernadotte.*" }
    pattern {
        H [form="Carl"];
        N [form="prins"];
        H -[APP]-> N;
        e: H -[ATR|DET]-> D;
        N < H; D << H;
    }
    commands {
        add_edge e2: N -> D; e2.label = e.label; del_edge e;
    }
}

%4351	 Og generelt er det i alle fall slik at private penger er [A1=mer] forgjengelige ( og kan ha [H=flere] føringer ) [A2=enn] offentlige penger .
rule ADV_head_attachment {
    global {sent_id = "004351"}
    pattern {
        A1 [form="mer"];
        A2 [form="enn"];
        A1 << A2;
        H [form="flere"];
        e: H -[ADV]-> A2;
    }
    commands {
        add_edge f: A1 -> A2;
        f.label = e.label;
        del_edge e;
    }
}

% 417	 Og i sommer var en ny legespire intervjuet i Nettavisen - den 18-årige supereleven Faisal Qureshi , som har sanket 25 seksere på videregående skole .

%  004197 "[...] ( Norges spesialetterforsker A1 / dommer ved A2 ) , [...]"
rule APP_to_KOORD {
    global { sent_id = "004197" }
    pattern {
        e: H -[APP]-> D ;
        H [form="spesialetterforsker"];
        D [form="dommer"];
    }
    commands {
        e.label = KOORD;
    }
}

% 016964: Det virker som fotball er det siste profesjonelle miljø hvor du ikke kan komme ut av skapet stolt. ( ... ) .
rule fix_postag_X {
    global {sent_id = "016964"}
    pattern { N [form="stolt.", upos=X]; R [upos=VERB];}
    with { * -[FINV|root]-> R; }
    commands {
        N.upos=ADJ;
        N.lemma="stolt";
        N.Degree="Pos";
        N.Definite="Ind";
        N.Number="Sing";
    }
}


% 004797: Og sjølv om aktørane i boka påfallande ofte seier hø-hø-hø, og nokre gonger hå-hå, er her ikkje eigentleg humor, slik at boka kan forklarast som parodi eller karikatur.
rule adverbial_formal_subject {
    pattern { N [upos=ADV, form="her"]; e: V -[FSUBJ]-> N;}
    commands { e.label = ADV; }
}

rule adverbial_interjection {
    pattern { D [upos=INTJ]; e: H -[ADV]-> D; }
    commands { D.upos = ADV;  }
}


% 76	 Alle maktene og myndigheitene er " skapt av , ved , til Kristus , " altså vert dei " sett på sin rette stad " ( ikkje ulikt Rom 13 , der Paulus understrekar at myndigheiter er " Guds tenarar " ) .
rule internal_punctuation_attachment {
    global { sent_id = "000076"}
    pattern {
        N [lemma="Kristus"]; P1 [form=","]; P2 [upos=PUNCT]; P1 < P2; e1: H1 -> P1;
        A [lemma="altså"]; V1 [form="vert"]; V2 [form="sett"]; e2: V2 -> A;
    }
    commands {
        del_edge e1; add_edge N -[IK]-> P1;
        del_edge e2; add_edge V1 -[ADV]-> A;
    }
}

% 13399	 Påstandane om stor kriminalitet blir også kontant avvist av mottaksleiaren .
rule fix_postag_INTJ {
    global {sent_id = "013399"}
    pattern {N[form ="mottaksleiaren", upos=INTJ]; *-[PUTFYLL]-> N;}
    commands {
        N.upos=NOUN;
        N.lemma="mottaksleiar";
        N.Gender="Masc";
    }
}


% 9674	 [...] "«dei 20 mest dedikerte og pålitelege konservative / patriotiske leiarar i landet»."
rule fix_coordinated_attributes {
    global { sent_id = "009674" }
    pattern {
        A1 [upos=ADJ];
        S [upos=SYM];
        A2 [upos=ADJ];
        N [upos=NOUN];
        A1 < S; S < A2; A2 < N;
        e: N -[ATR]-> A2;
        A1 -[IK]-> S;
    }
    commands {
        del_edge e;
        add_edge f: A1 -> A2; f.label=KOORD;
    }
}

% 000100: ein "levande tradisjon,"
% 000113: "på vegen,"
rule fix_non_projective_punctuation {
    global { sent_id = "000100" | "000113" | "000179" | "000582" | "010128" }
    pattern {
        C [lemma="$,"];
        Q [lemma="$\""];
        e: H1 -[IK]-> C;
        H2 -[IK]-> Q;
        C < Q;  % The comma immediately precedes the quotation sign
    }
    commands {
        del_edge e;
        add_edge H2 -[IK]-> C;
    }
}

% 002382:  «Det legemeldte sykefraværet gikk opp med 2,8 prosent fra 2. kvartal 2010 til 2. kvartal 2011», skriv NAV i ei pressemelding som nett vart send ut.
% Fix warning [L3 Warning flat-foreign-upos-feats] The child of a flat:foreign relation should have UPOS X and Foreign=Yes (but no other features).
rule make_bokmaal_citation_ukjent_1 {
    global { sent_id = "002382" }
    pattern {
        GOV  -[FLAT]-> DEP;
        DEP [ upos = ADJ, Number ];
    }
    commands {
        DEP.upos = X;
        del_feat DEP.Number;
    }
}

% 002382:  «Det legemeldte sykefraværet gikk opp med 2,8 prosent fra 2. kvartal 2010 til 2. kvartal 2011», skriv NAV i ei pressemelding som nett vart send ut.
rule make_bokmaal_citation_ukjent_2 {
    global { sent_id = "002382" }
    pattern {
        GOV  -[FLAT]-> DEP;
        DEP [ upos=NUM, NumType, Number ];
    }
    commands {
        DEP.upos = X;
        del_feat DEP.NumType;
        del_feat DEP.Number;
    }
}

% 010781:  Viktige aspekter ved dette er at Språkrådet må ha hele samfunnet som operasjonsfelt, og at det skal legges opp til samarbeid med aktuelle aktører over et bredt spekter, dessuten at observasjon og analyse må utgjøre en basis for arbeidet.»
rule make_bokmaal_citation_ukjent_3 {
    global { sent_id = "010781" }
    pattern {
        DEP [ upos=NOUN, Definite, Gender ];
        GOV -[FLAT]-> DEP;
    }
    commands {
        DEP.upos = X;
        del_feat DEP.Definite;
        del_feat DEP.Gender;
    }
}


% 010128:	 Kanskje gjentek serien seg sjølv ( Ralles nye kjærast Danuta ( eg nektar å tru at det blir dei to til slutt ! ) ...) ...
rule fix_non_projective_punctuation_2 {
    global { sent_id = "010128" }
    pattern {
        e: V1 -[PAR]-> CL;
        CL[lemma="nekte"];
        V2 -[SUBJ]-> N;
        N [form="Danuta"];
    }
    commands {
        del_edge e;
        add_edge N -[PAR]-> CL;
    }
}

% 010128: ... blir i alle fall forsøkt sjekka opp av sjefen på gartneriet der dei har sommarjobb ...
rule fix_non_projective_punctuation_3 {
    global { sent_id = "010128" }
    pattern {
        * -[INFV]-> V1;
        e: V2 -[ADV]-> A;
        V2 [lemma="sjekke"];
        A [form="i"];
        V1 [lemma="forsøkje"];
        A << V1;
        V1 < V2;
    }
    commands {
        del_edge e;
        add_edge V1 -[ADV]-> A;
    }
}

% 004431: "Krafta burde gått til det føremålet ho var tenkt , og avtalen burde ha falle bort da Union vart nedlagt."
% 016563: "Mange er nok overraska over at Odd-Bjørn Hjelmeset (Fjellhug/Vereide-Hyen) ikkje vart utteken i dag."
rule adjectival_participle {
    global { sent_id = "004431" | "016563" }
    pattern {
        V [ lemma = "verte" ];
        A [ form = "nedlagt"|"utteken", upos = ADJ, !VerbForm];
        V -[INFV]-> A;
    }
    commands { A.VerbForm = Part }
}

rule article_determiners {
    pattern {
      X [
          upos = DET,
          PronType=Dem,
          form = "Det"|"Den"|"Dét"|"Dén"|"De"|"Dem"|"D."|"det"|"den"|"dét"|"dén"|"de"|"dem"|"d."|"Dei"|"dei"
      ]
    }
    commands {
      X.PronType = Art;
    }
}

rule singular_determiners_pronoun_lemma {
    pattern {
      X [
          upos = DET|PRON,
          Number=Sing,
          lemma = "Det"|"Den"|"Dét"|"Dén"|"D."|"det"|"dét"|"dén"|"d."
      ]
    }
    commands {
      X.lemma = "den";
    }
}


rule singular_abbreviated_pronoun_lemma {
    pattern {
      X [
          upos = PRON,
          Abbr=Yes,
          lemma = "D."|"d."
      ]
    }
    commands {
      X.lemma = "den";
    }
}


rule plural_determiners_lemma {
    pattern {
      X [
          upos = DET,
          Number=Plur,
          lemma = "De"|"Dem"|"dem"|"d."|"D."
      ]
    }
    commands {
      X.lemma = "de";
    }
}

rule plural_pronoun_lemma {
    pattern {
      X [
          upos = PRON,
          Number=Plur,
          Person=3,
          lemma = "De"|"Dem"|"dem"|"d."|"D."
      ]
    }
    commands {
      X.lemma = "de";
    }
}

rule nynorsk_determiner_pronoun_lemma {
    pattern {
      X [
          upos = DET|PRON,
          Number=Plur,
          lemma = "Dei"
      ]
    }
    commands {
      X.lemma = "dei";
    }
}


rule remove_verb_feats_from_adj{
    global { sent_id = "002192" }
    pattern {
        N [upos=ADJ, Mood, Tense, VerbForm];
    }
    commands{
        del_feat N.Mood;
        del_feat N.Tense;
        del_feat N.VerbForm;
        N.Degree = "Pos";
    }
}


rule fix_verb_features {
    global {sent_id = "001967"}
    pattern {
        N [upos=VERB, Definite, Degree, Gender, Mood, Tense, VerbForm=Fin]
    }
    commands {
        del_feat N.Definite;
        del_feat N.Degree;
        del_feat N.Definite;
        del_feat N.Degree;
        del_feat N.Gender;
        del_feat N.Mood;
        del_feat N.Tense;
        N.VerbForm = "Part";
    }
}
