% Post-fix errors introduced by udapy fixes

rule name_compound_punctuation {
  pattern { DEP [upos=PUNCT]; e: GOV -[^punct]-> DEP;}
  commands {e.label = punct}
}


% FIXME: Check if this can be removed, since a relabelling rule handles PSUBJ to csubj
%018806	 For helt ærlig , det passer jo aldri å være hjemme i permisjon .
rule remove_PSUBJ {
  global {
    sent_id = "018806";
    text = re".*det passer jo aldri å være hjemme i permisjon.*"
  }
  pattern {
    e: H -[PSUBJ]-> N;
  }
  commands {
    e.label = csubj;
  }
}

% Adhoc postfix. TODO: rearrange the copula, lex, aux and root shifting rules so this problem doesn't arise.
% 008474 Vi blir spurt ofte om det er aktuelt med en europeisk NHL-divisjon , men det ligger ikke i planene våre for den nærmeste framtid .
rule shift_head_ {
  global { sent_id = "008474" }
  pattern {
    V [lemma="være"];
    P[upos=ADJ];
    e: H -[aux]-> V;
    e1: V -[xcomp]-> P;
    e2: V -[nsubj]-> O;
  }
  with {V -[mark]-> *;}
  commands {
    add_edge f: P -> V; f.label = cop;
    shift_out V ==> P;
    del_edge e;
    del_edge e1;
    add_edge f1: H -> P; f1.label = ccomp;
    del_edge e2;
    add_edge f2: P -> O; f2.label = obl;
  }
}

% Adhoc postfix. TODO: rearrange the copula, lex, aux and root shifting rules so this problem doesn't arise.
% 009431 Jeg har levd som en 30-40-åring siden jeg var 20 , så det var deilig å få være ei ung jente , en nybegynner , igjen .
rule shift_koordinating_predicate {
  global {sent_id = "009431"}
  pattern {
    V [lemma="være"];
    P [upos=ADJ];
    e: H -[aux]-> V;
    e1: V -[xcomp]-> P;
    e2: V -[nsubj]-> O;
  }
  with {V -[cc]-> *;}
  commands {
    add_edge f: P -> V; f.label = cop;
    shift_out V ==> P;
    del_edge e;
    del_edge e1;
    add_edge f1: H -> P; f1.label = conj;
    del_edge e2;
    add_edge f2: P -> O; f2.label = csubj;
    shift_out V ==> P;
  }
}

% Adhoc postfix. TODO: rearrange/rewrite the copula, lex, aux and root shifting rules so this problem doesn't arise.
% 017771: Representanter for SV har til og med uttrykt at det er et «samfunnsproblem» med friskoler.
rule restructure_copula_comp_clause {
  global {sent_id = "017771"}
  pattern {
    V [lemma="være"];
    P [upos=NOUN];
    e: H -[aux]-> V;
    e1: V -[xcomp]-> P;
    e2: V -[nsubj]-> O;
  }
  commands {
    add_edge f: P -> V; f.label = cop;
    shift_out V ==> P;
    del_edge e;
    del_edge e1;
    add_edge f1: H -> P; f1.label = ccomp;
    del_edge e2;
    add_edge f2: P -> O; f2.label = nmod;

  }
}

% adhoc postfix:
% 5103	 Her er forresten meg og Paradise Hotel-Are og Cult-promotør / gatebil-Iselin .
rule appos_to_conj {
  global {sent_id ="005103"}
  pattern {
    e: H -[IK]-> S;
    e1: H -[appos]-> N;
  }
  commands {
    e1.label = conj;
    del_edge e;
    add_edge f: N -> S; f.label = cc;
  }
}


% 007590
% Hver for seg går jentene til en de tror kan hjelpe dem, setter seg ned i kontorstolen hos fastlegen sin, og forteller.
rule change_det_to_obl {
    global { sent_id = "007590" }
    pattern {
        X [upos=DET];
        Y [upos=ADP];
        Z [upos=PRON];
        ROT -[nsubj]-> NOM;
        e: NOM -[det]-> X;
        X-[nmod]-> Z;
        Z -[case]-> Y;
    }
    commands {
      add_edge f: ROT -> X;
      f.label = obl;
      del_edge e;
    }
}


rule conj_left_to_right {
  global { sent_id <> "015775" }
  pattern {
      HEAD -> N1;
      e: HEAD -[conj]-> N2;
      N2 -[cc]-> C;
      N1 << N2;
      N1 < C;
      N2 << HEAD;
  }
  commands {
    add_edge f: N1 -> N2;
    f.label = conj;
    del_edge e;
  }
}

rule conj_left_to_right_with_dashes {
  global { sent_id <> "015775" }
  pattern {
      HEAD -> N1;
      e: HEAD -[conj]-> N2;
      N2 -[cc]-> C;
      N1 << N2;
      P [upos=PUNCT];
      P < C;
      N1 < P;
      N2 << HEAD;
  }
  commands {
    add_edge f: N1 -> N2;
    f.label = conj;
    del_edge e;
  }
}
