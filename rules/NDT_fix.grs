% 000890: "Men det har vi ikke klart å vise i de siste kvalifiseringene, sier Portsmouth-spilleren."
rule double_subject {
    pattern {
        V [form="klart", upos="VERB"];
        D [form="det", upos="PRON"];
        e: V -[SUBJ]-> D;
        R [form="har", upos="AUX"];
        * -[FINV]-> R;
    }
    commands {
        del_edge e;
        add_edge f: R -> D;
        f.label = FSUBJ;
    }
}

% 10996: "Ikke det at boka mi skal være en fasit , men den kan være underlag for å diskutere ."
% Dobbelt subjekt dersom "den" beholder "SUBJ"-relasjonen
% 8128: "- At andre regissører kaller deg pompøs ; kan det også skyldes misunnelse ?"
% SUBJ -> FSUBJ; Mulig analyse som setningsantisiperende hvis leddsetningene var omrokkert.
rule relabel_formal_subject {
    global { sent_id = "008128" | "010996"}
    pattern {
        N1 [upos="PRON"];
        N2 [upos="AUX"];
        e: N2 -[SUBJ]-> N1;
    }
    with { e.length = 1; }
    commands {
        e.label = FSUBJ;
    }
}

% 007036: "(...) å utgi vår neste plate på CD - og (...)"
rule DET_to_NOUN {
    pattern {
        N [form="CD", upos="DET"];
        A [upos=ADP];
        e: A -[IK]-> N;
    }
    commands {
        N.upos = "NOUN";
        e.label = "PUTFYLL";
    }
}


% 001021: "Eva Joly + Daniel Cohn-Bendit = EU"
rule PAR_to_KOORD {
  pattern {
    N1 [lemma="Eva"];
    N3 [lemma="EU"];
    S [form="=", upos=SYM];
    e: N1 -[PAR]-> N3;
  }
  with {
    N1 << S; S < N3; % The symbol immediately precedes the coordinated node
  }
  commands {
    e.label = KOORD;
  }
}


% Eksempel 000016: Først i prosesjonen gikk kong Harald og brudens mor , Marit Tjessem.
rule add_morph_feats {
    pattern {
        N [upos=NOUN, lemma="kong", !Definite, !Gender, !Number];
        * -[APP]-> N;
    }
    commands {
        N.Definite=Ind;
        N.Gender=Masc;
        N.Number=Sing;
    }
}

% 5092	 Til informasjon , så veier jeg omtrent 65 kilo - 63 kilo dersom jeg vasker meg .
rule shift_APP {
    global { sent_id = "005092" }
    pattern {
        A [form="så"];
        e1: V -[APP]-> A;
        e2: A -[ADV]-> N;
    }
    commands {
        e1.label = "ADV";
        e2.label = "APP";
    }

}
