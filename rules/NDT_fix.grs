% 000890: "Men det har vi ikke klart å vise i de siste kvalifiseringene, sier Portsmouth-spilleren."
rule double_subject {
    global {
        sent_id = "000890";
        text = re".*det har vi ikke klart å vise.*"
    }
    pattern {
        V [form="klart", upos="VERB"];
        D [form="det", upos="PRON"];
        e: V -[SUBJ]-> D;
        R [form="har", upos="AUX"];
        * -[FINV]-> R;
    }
    commands {
        del_edge e;
        add_edge f: R -> D;
        f.label = FSUBJ;
    }
}

% 8128: "- At andre regissører kaller deg pompøs ; kan det også skyldes misunnelse ?"
% SUBJ -> FSUBJ; Mulig analyse som setningsantisiperende hvis leddsetningene var omrokkert.
rule relabel_formal_subject {
    global {
        sent_id = "008128";
        text = re".*andre regissører kaller deg pompøs.*"
    }
    pattern {
        N1 [upos="PRON"];
        N2 [upos="AUX"];
        e: N2 -[SUBJ]-> N1;
    }
    with { e.length = 1; }
    commands {
        e.label = FSUBJ;
    }
}

% 007036: "(...) å utgi vår neste plate på CD - og (...)"
rule DET_to_NOUN {
    global {
        sent_id = "007036";
        text = re"utgi vår neste plate på CD"
    }
    pattern {
        N [form="CD", upos="DET"];
        A [upos=ADP];
        e: A -[IK]-> N;
    }
    commands {
        N.upos = "NOUN";
        e.label = "PUTFYLL";
    }
}


% 001021: "Eva Joly + Daniel Cohn-Bendit = EU"
rule PAR_to_KOORD {
    global {
        sent_id = "001021";
        text = re"Daniel Cohn-Bendit";
    }
    pattern {
    N1 [lemma="Eva"];
    N3 [lemma="EU"];
    S [form="=", upos=SYM];
    e: N1 -[PAR]-> N3;
  }
  with {
    N1 << S; S < N3; % The symbol immediately precedes the coordinated node
  }
  commands {
    e.label = KOORD;
  }
}


% Eksempel 000016: Først i prosesjonen gikk kong Harald og brudens mor , Marit Tjessem.
rule add_morph_feats {
    pattern {
        N [upos=NOUN, lemma="kong", !Definite, !Gender, !Number];
        * -[APP]-> N;
    }
    commands {
        N.Definite=Ind;
        N.Gender=Masc;
        N.Number=Sing;
    }
}

% 5092	 Til informasjon , så veier jeg omtrent 65 kilo - 63 kilo dersom jeg vasker meg .
rule shift_APP {
    global {
        sent_id = "005092";
        text = re".*så veier jeg omtrent 65 kilo.*"
    }
    pattern {
        A [form="så"];
        e1: V -[APP]-> A;
        e2: A -[ADV]-> N;
    }
    commands {
        e1.label = "ADV";
        e2.label = "APP";
    }

}


% 10996: "Ikke det at boka mi skal være en fasit , men den kan være underlag for å diskutere ."
% Dobbelt subjekt dersom "den" beholder "SUBJ"-relasjonen
rule APP_to_ADV {
    global {
        sent_id = "010996";
        text = re"Ikke det at boka mi skal være en fasit.*"
    }
    pattern {
        N1 [lemma="ikke"];
        N2 [form="det"];
        e: N2 -[APP]-> N1;
    }
    commands {
        e.label = ADV;
    }
}

% Prosesjonen ble avsluttet av den 90 år gamle prins Carl Bernadotte som førte brudgommens søster , Märtha Louise , til bords .
rule APP_modifiers {
    global { text = re".*den 90 år gamle prins Carl Bernadotte.*" }
    pattern {
        H [form="Carl"];
        N [form="prins"];
        H -[APP]-> N;
        e: H -[ATR|DET]-> D;
        N < H; D << H;
    }
    commands {
        add_edge e2: N -> D; e2.label = e.label; del_edge e;
    }
}


% 417	 Og i sommer var en ny legespire intervjuet i Nettavisen - den 18-årige supereleven Faisal Qureshi , som har sanket 25 seksere på videregående skole .
