import "NDT_fix.grs"
include "appositions.grs"
include "reverse_heads.grs"
import "relabel_NDT_to_UD_deprel.grs"




%%% Copied and modified from https://github.com/surfacesyntacticud/tools/blob/master/converter/grs/SUD_to_UD.grs#L134
% Feature "nom" is used to deal with all nominals in a uniform way.
package nom_init {
% All adverbs, numbers and symbols that are direct objects are considered nominal.
  rule nom_comp {
    pattern {A[upos=ADJ|ADV|INTJ|NUM|SYM,!nom]; H -[1=DOBJ]-> A}
    without{ H[upos=ADP|SCONJ]; A[upos=ADJ|ADV]}
    without{A[upos=ADV]; }
    commands {A.nom=y}
  }

% All words with a determiner are considered nominal.
  rule nom_det {
    pattern {N[upos<>NOUN|PRON|PROPN|X,!nom]; N -[DET]-> *}
    commands {N.nom=y}
  }

% All words with NOUN, PRON, PROPN, SYM, X as POS are considered nominal.
  rule nom_noun {
    pattern {N[upos=NOUN|PRON|PROPN|X,!nom]}
    commands {N.nom=y}
  }

% All numerals following their governor are considered nominal.
  rule nom_num_after {
    pattern {N[upos=NUM,!nom]; H -> N; H << N}
    commands {N.nom=y}
  }

% All numerals that are verb modifiers or prepositional complements are considered nominal.
  rule nom_num_mod {
    pattern {N[upos=NUM,!nom]; H[upos=ADP|AUX|VERB]; H -[ADV|PUTFYLL]-> N}
    commands {N.nom=y}
  }

% All adverbs, numbers and symbols that are subjects are considered nominal.
  rule nom_subj {
    pattern {A[upos=ADJ|ADV|SYM|NUM,!nom]; H -[SUBJ]-> A}
    commands {A.nom=y}
  }

% All determiners that have outgoing ATR relations are considered nominal
  rule nom_ATR_dep {
    pattern { N[upos=DET,!nom]; N -[ATR]-> A;}
    commands { N.nom=y}
  }
% Adjectives that are APP dependents on a proper noun are nominals
  rule nom_adj_app_propn {
    pattern { N [upos=PROPN|NOUN]; A [upos=ADJ,!nom]; N -[APP]-> A;}
    commands {A.nom=y}
  }


}


package final {
  rule del_nom{
    pattern {W[nom=y]}
    commands {del_feat W.nom}
  }

}


strat main {
    Seq (
        Onf (NDT_fix),
        Onf (nom_init),
        rev_main, % reverse_heads
        APP_main,  % appositions strategy, restructuring and relabelling APP relations
        Onf (relabel_NDT_to_UD_deprel),
        Onf (final),
    )
}
