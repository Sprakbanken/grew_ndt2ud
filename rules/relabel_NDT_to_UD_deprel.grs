% relabel edges: Change NDT dependency relation to UD deprel with 1 to 1 mappings

rule punctuation_sentence_end {
    pattern { e: N -[ IP ]-> P }
    commands { del_edge e; add_edge N -[ punct ]-> P }
}

rule punctuation_mid_sentence {
    pattern { e: N -[ IK ]-> P;  }
    commands { del_edge e; add_edge N -[ punct ]-> P }
}

rule nominal_subject {
    pattern { V [upos=VERB]; N [upos=PROPN|NOUN]; e: V -[SUBJ]-> N; }
    commands { e.label= nsubj}
}

rule expletive_subj {
    pattern { e: GOV -[ FSUBJ ]-> DEP }
    commands { del_edge e; add_edge GOV -[ expl ]-> DEP  }
}

rule expletive_obj {
    pattern { e: GOV -[ FOBJ ]-> DEP }
    commands { del_edge e; add_edge GOV -[ expl ]-> DEP  }
}

rule indirect_obj {
    pattern { e: GOV -[ IOBJ ]-> DEP }
    commands { del_edge e; add_edge GOV -[ iobj ]-> DEP  }
}

rule obj_predicative {
    pattern { e: GOV -[ OPRED ]-> DEP }
    commands { del_edge e; add_edge GOV -[ xcomp ]-> DEP  }
}

rule interjection {
    pattern { e: GOV -[ INTERJ ]-> DEP }
    commands { del_edge e; add_edge GOV -[ discourse ]-> DEP  }
}

rule coordinating_conjuction {
    pattern { e: GOV -[ KONJ ]-> DEP }
    commands { del_edge e; add_edge GOV -[ cc ]-> DEP  }
}

rule conjunct {
    pattern { e: GOV -[ KOORD ]-> DEP }
    commands { del_edge e; add_edge GOV -[ conj ]-> DEP  }
}

rule coordination_verbal_ellipsis {
    pattern { e: GOV -[ KOORD-ELL ]-> DEP }
    commands { del_edge e; add_edge GOV -[ orphan ]-> DEP  }
}

% presenteringssetninger: når "være" er rot og setningen har PSUBJ og FSUBJ, men ikke SPRED. Behold roten.
rule presentational_expl {
  pattern {
    H [ lemma="være" ];
    e1: H -[PSUBJ]-> D1;
    e2: H -[FSUBJ]-> D2;
  }
  without {
    H -[SPRED]-> *;
  }
  commands {
    e1.label = nsubj;
    e2.label = expl;
  }
}

% In the case that the lexical verb is already the head:
% "være" and "ha" can both be temporal auxiliaries in a passive construction, but only "bli" gets the "aux:pass" relation
rule passive_lex_aux_bli {
  pattern {
    H [ upos=AUX, lemma="bli" ];
    D [ upos=VERB, VerbForm=Part ];
    e: D -> H;
  }
  without {
    e.label = "aux:pass";  % ensure commands are effective
  }
  commands {
    e.label = "aux:pass";
  }
}

rule temporal_aux_vare_ha {
  pattern {
    A [ upos=AUX, lemma="være"|"ha" ];
    V [ upos=VERB|AUX, VerbForm=Part ];
    e: V -> A;
  }
  without {
    e.label = "aux";
  }
  commands {
    e.label = "aux";
  }
}

% possibly redundant renaming rule
rule passive_verb_form {
    pattern {
        V [upos=VERB, Voice=Pass, form=re".*s"];
        e: V -[SUBJ]-> N;
    }
    commands {
        e.label= nsubj;
     }
}

